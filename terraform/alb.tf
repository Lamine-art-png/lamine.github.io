# ALB security group (public 80/443)
resource "aws_security_group" "alb" {
  name_prefix = "${var.project}-alb-"
  vpc_id      = data.aws_vpc.default.id

  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }
  ingress {
    from_port   = 443
    to_port     = 443
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = local.tags
}

# ECS tasks security group (no public ingress)
resource "aws_security_group" "ecs_tasks" {
  name_prefix = "${var.project}-ecs-tasks-"
  vpc_id      = data.aws_vpc.default.id

  # outbound allowed (for pulling images, calling APIs, etc.)
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = local.tags
}

# Allow ALB -> ECS tasks on the app port
resource "aws_security_group_rule" "alb_to_tasks" {
  type                     = "ingress"
  from_port                = var.container_port
  to_port                  = var.container_port
  protocol                 = "tcp"
  security_group_id        = aws_security_group.ecs_tasks.id
  source_security_group_id = aws_security_group.alb.id
}
